// Generic cards usable by ANY monster

Class CCard_AdditionalMonster : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = true;
	effectMonsters = true;
	description = "All " .. GetMonsterName() .. " in the level are increased by one.";
	background = "CARB01";
	canStack = true;
	addtag("startspawn");
	}
	
	override bool Condition()
	{
		if(monsterFigure.bBOSS)
		{
			return false;
		}
		
		return true;
	}

	uint count;

	override void PreGameStart()
	{
		count = 0;
	}

	override void OnGameStart(Actor a)
	{
		if(CCards_Functions.HasMapSpecial(monsterType) || count >= 100)
		{
			return;
		}

		count++;

		Actor clone = a.Spawn(a.GetClassName(), a.pos);
		if(clone)
		{
			clone.bDONTOVERLAP = false;
			clone.angle = a.angle + random(0,360);
			clone.bAMBUSH = a.bAMBUSH;
			
			if(clone.bSOLID)
			{
				clone.GiveInventory("CCards_Actor_SolidHelper", 1);
			}
		}
	}
}

Class CCard_RespawningMonster : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = true;
	effectMonsters = true;
	description = GetMonsterName() .. " respawns after death.";
	background = "CARB04";
	canStack = false;
	priority = 1000;
	AddTag("causesresurrection");
	addtag("monsterability");
	}
	
	bool isSpecialMap;
	
	override bool Condition()
	{		
		if(game.G_SkillPropertyInt(SKILLP_RESPAWN))
		{
			return false;
		}
		
		if(monsterFigure.default.health < 150 || monsterFigure.bBOSS || CCards_Functions.HasMapSpecial(monsterType))
		{
			return false;
		}

		return true;
	}

	override void OnGameStart(Actor a)
	{
		ThinkerIterator ti = ThinkerIterator.Create("CCards_RespawnManager");
				
		CCards_RespawnManager r;
			
		r = CCards_RespawnManager(ti.Next());

		if(!r)
		{
			r = CCards_RespawnManager(new ("CCards_RespawnManager"));
		}

		if((a.bBOSS || a.bBOSSDEATH) && isSpecialMap)
		{
			return;
		}

		r.AddMonster(a);
	}
}

Class CCards_RespawnManager : Thinker
{
	Array<CCards_MonsterRespawner> monsters;

	void AddMonster (Actor a)
	{
		if(CCards_Functions.HasMapSpecial(a.GetClass()))
		{
			return;
		}

		CCards_MonsterRespawner m = CCards_MonsterRespawner(new ("CCards_MonsterRespawner"));
		m.monster = a;
		m.pos = a.pos;
		m.monstertype = a.GetClass();
		monsters.Push(m);
	}

	override void Tick()
	{
		if((Level.Time % 35) != 0)
		{
			return;
		}

		for(int i; i<monsters.Size(); i++)
		{
			if(monsters[i].monster && monsters[i].monster.CountInv("CCards_Actor_HasRespawned") > 0)
			{
				continue;
			}

			if(monsters[i].monster && monsters[i].monster.health < 1 && !monsters[i].isdead)
			{
				monsters[i].isDead = true;
				monsters[i].timer = (4 * ((random(10, 60)) + (monsters[i].monster.default.health / 50)));
				continue;
			}

			if(monsters[i].isdead)
			{
				monsters[i].timer--;

				//Console.Printf("" .. monsters[i].timer);
				if(monsters[i].timer<1)
				{
					//Console.Printf("time to respawn!");
					if(monsters[i].monster)
					{
						if(monsters[i].monster.health<1)
						{
							monsters[i].monster.A_SetScale(monsters[i].monster.default.scale.x, monsters[i].monster.default.scale.y);
							//monsters[i].monster.A_SetTranslation(monsters[i].monster.default.Translation);
							monsters[i].monster.A_Respawn(RSF_KEEPTARGET|RSF_FOG);
							monsters[i].monster.GiveInventory("CCards_Actor_HasRespawned", 1);
							if(monsters[i].monster.bCOUNTKILL)
							{
								monsters[i].monster.ClearCounters();
							}
							continue;
						}
						else
						{
							monsters[i].isdead = false;
						}
					}
					
					if(!monsters[i].monster)
					{
						Actor a = Actor.Spawn(monsters[i].monstertype, monsters[i].pos);

						if(a)
						{
							a.Spawn("TeleportFog", a.pos);
							a.angle = random(0,360);
							monsters[i].monster = a;
							monsters[i].isdead = false;
							a.GiveInventory("CCards_Actor_ForcedRespawn", 1);
							a.GiveInventory("CCards_Actor_HasRespawned", 1);
							if(monsters[i].monster.bCOUNTKILL)
							{
								monsters[i].monster.ClearCounters();
							}
						}
					}
				}
			}
		}
	}
}

Class CCards_MonsterRespawner
{
	Actor monster;
	Class<Actor> monstertype;
	Vector3 pos;
	uint timer;
	bool isdead;
}

Class CCards_Actor_HasRespawned : Inventory {}

Class CCard_MonsterGang : GenericCard
{
	mixin WorldEvent;

	CCards_Actor_SafeSpawner spawner;
	bool isSpawning;
	bool finished;
	Vector3 pos;
	
	override void Init()
	{
	Super.Init();
	effectWorld = true;
	description = "A gang of " .. GetMonsterName() .. " may appear.";
	background = "CARB17";
	canStack = true;
	addtag("newspawn");
	addtag("monstergang");
	}
	
	override bool Condition()
	{	
		if(monsterFigure.bBOSS)
		{
			return false;
		}
		return true;
	}
	
	override void OnGameStart(actor a)
	{
		SetEventTotals();

		isSpawning = false;
		finished = false;
		spawner = null;
		
		if(CCards_Functions.HasMapSpecial(monsterType))
		{
			finished = true;
		}
	}
	
	override void OnTick(actor a)
	{
		if(finished)
		{
			return;
		}
		
		if(EventTrigger() && !isSpawning)
		{
			spawner = CCards_Safespawner.New("", 1, "", GetDefaultByType(monsterType).radius, GetDefaultByType(monsterType).height, 0, 0);
			isSpawning = true;
		}
		
		if(isSpawning)
		{
			if(spawner)
			{
				pos = spawner.pos;
			}
			else
			{
				CCards_Actor_GangPortal p = CCards_Actor_GangPortal(Actor.Spawn("CCards_Actor_GangPortal", pos));
				CCards_Functions.CardAlert("\ciA gang of monsters is spawning!", "corruptioncards/cube", 0);
				finished = true;
				
				p.monster.Push(monsterType);

				uint amount = 25;
				
				amount -= (GetDefaultByType(monsterType).health / 40);

				p.spawncount = Max(5, amount);
				//p.A_LogInt(p.spawncount);
			}
		}
	}
}

class CCards_Actor_GangPortal : Actor
{
	Default
	{
		-SOLID;
		+NOINTERACTION;
		+BRIGHT
		scale 1.5;
	}
	
	int spawncount;
	Array<Class<Actor> > monster;
	GenericBuffCard buffcard;
	bool mini, changepos;

	Actor newmon;
	
	states
	{
		Spawn:
		TNT1 A 1;
		TNT1 A 0
		{			
			target = CCards_Functions.RandomPlayer();
		}		
		Portal:
		TNT1 A 0
		{
			if(monster.Size() < 1)
			{
				SetStateLabel("Death");
				return;
			}


			uint r = random(0, monster.Size()-1);

			if(monster[r])
			{
				int rad = GetDefaultByType(monster[r]).radius;
				int hei = GetDefaultByType(monster[r]).height;

				if(mini)
				{
					rad = rad * 0.6;
					hei = hei * 0.6;
				}
				A_SetSize(Max(radius, rad), Max(radius, hei), false);
			}

			
			Actor clone = Spawn(monster[r], pos);//A_SpawnItemEx(monster);
			if(clone)
			{	
				if(mini)
				{
					clone.GiveInventory("CCards_Actor_MiniMonsterPowerup", 1);
					clone.bFULLVOLSEE = false;
					clone.bNOINFIGHTING = true;
				}

				if(changepos)
				{
					clone.GiveInventory("CCards_Actor_ResurrectionTeleport", 1);
				}

				if(buffcard)
				{
					buffcard.Buff(clone);
				}

				if(target)
				{
				clone.LastHeard = target;
				clone.target = target;
				}
				if(clone.bSOLID)
				{
					clone.GiveInventory("CCards_Actor_SolidHelper", 1);
				}
				
				spawncount--;
				
				if(spawncount == 0)
				{
				SetStateLabel("Death");
				}
				//A_AlertMonsters(0, 0);
				
			}
			A_StartSound("misc/teleport");

			NewRandomPos();
		}
		C_F0 JKLMLK 4;
		loop;
		Death:
		C_F0 JKJKLMNOPQRS 4;
		stop;
	}

	Array <Actor> positions;

	void NewRandomPos()
	{
		// needs big brain
	}

	override void PostBeginPlay()
	{
		if(CCards_Functions.FindCardByName("CCard_WarpPortals"))
		{
			changepos = true;
		}

		if(mini)
		{
			A_SetScale(1.0, 1.0);
		}
		CCard_EnchantedPortals p;

		p = CCard_EnchantedPortals(CCards_Functions.FindCardByName("CCard_EnchantedPortals"));

		if(!p)
		{
			return;
		}

		CCards_BuffManager ev = CCards_BuffManager(EventHandler.Find("CCards_BuffManager"));

		if(!ev)
		{
			return;
		}

		ev.FindBuffs();

		if(ev.buffs.Size() == 0)
		{
			return;
		}

		Object o = new (ev.buffs[random(0, ev.buffs.Size() - 1)]);
		buffcard = GenericBuffCard(o);
		A_SetTranslation("CCards_WarpTrapTele");
	}

	override void OnDestroy()
	{
		if(buffcard)
		{
			buffcard.Destroy();
		}
		return;
	}

	
}

Class CCard_ChildMonster : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = true;
	effectMonsters = true;
	description = GetMonsterName() .. " has a small companion.";
	background = "CARB22";
	canStack = true;
	addtag("startspawn");
	}
	
	uint count;

	override void PreGameStart()
	{
		count = 0;
	}

	override void OnGameStart(Actor a)
	{
		if(CCards_Functions.HasMapSpecial(monsterType) ||  count >= 100)
		{
			return;
		}

		count++;

		Actor clone = a.Spawn(a.GetClassName(), a.pos);
		
		if(clone)
		{
			clone.A_GiveInventory("CCards_Actor_MiniMonsterPowerup", 1);
			//clone.bTHRUSPECIES = true;
			clone.bDONTOVERLAP = false;
			
			clone.angle = a.angle + random(0,360);
			clone.bAMBUSH = a.bAMBUSH;
			
			if(clone.bSOLID)
			{
				clone.GiveInventory("CCards_Actor_SolidHelper", 1);
			}
		}
	}

}

Class CCards_Actor_MiniMonsterPowerup : PowerDamage
{
	Default
	{
		Damagefactor "normal", 0.75;
	}

	bool isdead;

	override void OwnerDied ()
	{
		isdead = true;
		//Destroy ();
	}

	override void Tick ()
	{
		if(!owner)
		{
			return;
		}
	}

	override void AttachToOwner(Actor a)
	{
		int newhp = a.default.health / 2;
		a.StartHealth = newhp / G_SkillPropertyFloat(SKILLP_MonsterHealth);
		a.health = newhp;
		a.SetInventory("CCards_Actor_MaxHealth", a.health);
		a.GiveInventory("CCards_Actor_HalfProjectileZOff", 1);
		Super.AttachToOwner(a);

	}

	override void DoEffect()
	{	
		if(!owner)
		{
			return;
		}

		if(owner.health>0 && isdead)
		{
			isdead = false;
			InitEffect();
		}

		Owner.A_SoundPitch(CHAN_VOICE, 1.15);

		float miniscale = 0.60;

		if(owner.scale.x != owner.default.scale.x * miniscale)
		{
			owner.A_SetScale(owner.default.scale.x * miniscale, owner.default.scale.y * miniscale);
		}

		if(owner.radius != owner.default.radius * miniscale)
		{
			owner.A_SetSize(owner.default.radius * miniscale, owner.default.height * miniscale);
		}
	}
}

Class CCard_MonsterAssassin : GenericCard
{
	mixin WorldEvent;

	CCards_Actor_SafeSpawner spawner;
	bool isSpawning;
	bool finished;
	Vector3 pos;

	int power;
	
	override void Init()
	{
	Super.Init();
	useSpecies = false;
	effectPlayers = true;
	description = "A " .. GetMonsterName() .. " will try to assassinate you.";
	background = "CARB23";
	canStack = true;
	cardtag = TAG_EVENT;
	addtag("addspecialmonster");
	}
	
	override bool Condition()
	{	
		if(monsterFigure.health < 200)
		{
			return false;
		}
		return true;
	}
	
	override void OnGameStart(actor a)
	{
		SetEventTotals();
		isSpawning = false;
		finished = false;
		spawner = null;
	}
	
	override void OnTick(actor a)
	{
		if(finished)
		{
			return;
		}
		
		if(CCards_Functions.HasMapSpecial(monsterType))
		{
			finished = true;
			return;
		}
		
		if(isSpawning)
		{
			ThinkerIterator ti = ThinkerIterator.Create("CCards_AssassinSpawn");
				
			CCards_AssassinSpawn t;
			
			while (t = CCards_AssassinSpawn(ti.Next()))
			{
				if(t.target)
				{
				t.target.health = t.target.health + ((t.target.health / 5) * Min(power, 5));
				t.target.GiveInventory("CCards_Actor_AssassinBuff", 1);
				//t.target.GiveInventory("CCards_Actor_TeleporterBuff", 1);
				t.target.SetTag(t.target.GetTag() .. " \cg(ASSASSIN!)");
				t.target.seesound = "";
				t.target.activesound = "";
				t.target.target = a;
				t.target.lastheard = a;
				t.target.bALWAYSFAST = true;
				t.target.bNOBLOCKMONST = true;
				t.target.bNOTARGETSWITCH = true;
				t.target.bNOINFIGHTING = true;
				t.target.bDONTSQUASH = true;
				t.target.bDONTMORPH = true;
				finished = true;
				power++;
				break;
				}
			}

			return;
		}
		
		if(EventTrigger())
		{
			spawner = CCards_Safespawner.New(monsterType.GetClassName(), 1, "CCards_AssassinSpawn", GetDefaultByType(monsterType).radius, GetDefaultByType(monsterType).height, 128, 0);
			if(a && spawner)
			{
				spawner.SetSpawnLocation(a);
			}
			isSpawning = true;
		}
	}
}

class CCards_Actor_AssassinBuff : PowerDamage
{
	Default
	{
		DamageFactor "Normal", 1.8;
		powerup.duration -99999;
	}
	
	override void DetachFromOwner()
	{
		effecttics = 0;
		if(owner && owner.health < 1)
		{
			CCards_Functions.CardAlert("\crAn assassin was killed!", "corruptioncards/assassindeath", 35*3);
		}
		Super.DetachFromOwner();
	}
}

class CCards_AssassinSpawn : Actor
{
	Default
	{
	+NOINTERACTION;
	+MISSILE;
	height 1;
	radius 1;
	renderstyle "style_none";
	}
	
	states
	{
	Spawn:
	PLAY A 35;
	stop;
	}
}


Class CCard_MonsterSentry : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = true;
	effectMonsters = true;
	background = "CARB56";
	canStack = false;
	AddTag("causesresurrection");
	AddTag("monsterability");
	}
	
	override bool Condition()
	{
		if(monsterFigure.bBOSS || !monsterFigure.FindState("Raise", true) || 
		monsterFigure.GetClassName() == "PainElemental" || 
		monsterFigure.GetClassName() == "Demon" ||
		monsterFigure.GetClassName() == "Spectre" ||
		CCards_Functions.HasMapSpecial(monsterType))
		{
			return false;
		}
		
		return true;
	}

	int count;

	override void OnGameStart(Actor a)
	{
		count -= random(0, 1);

		if(count < 1 && !a.bBOSS && a.FindState("Raise", true) && !CCards_Functions.ActorHasMapSpecial(a))
		{
			a.GiveInventory("CCards_Actor_SentryActivator", 1);
			a.bNOICEDEATH = true;
			count = 5;
		}
	}
}

class CCards_Actor_SentryDamageNerf : PowerDamage
{
	Default
	{
		Powerup.Duration -3;
		Damagefactor "normal", 0.6;
	}
}

class CCards_Actor_SentryActivator : Inventory
{
	override void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
	}

	bool issentry;
	bool hasdied;
	int timer;
	Vector2 anchorpos;

	override void Tick()
	{
		if(!owner)
		{
			Destroy();
			return;
		}

		if(owner.health < 1)
		{
			if(!hasdied)
			{
				hasdied = true;
				timer = random(35, 35*3) + ((owner.default.health / 200) * 35);
			}

			if(timer>0)
			{
				timer--;
			}
		}

		if(hasdied && timer < 1)
		{
			if(RaiseActor(owner))
			{
				owner.bNOICEDEATH = true;
				if(owner.default.bCOUNTKILL)
				{
					owner.A_ChangeCountFlags(0, 0, 0);
				}
				owner.GiveInventory("CCards_Actor_SentryDamageNerf", 1);
				hasdied = false;
			}
		}

		if(!issentry)
		{
			if(owner.target && owner.CheckSight(owner.target, SF_IGNOREVISIBILITY))
			{
				anchorpos = (owner.pos.x, owner.pos.y);
				issentry = true;
				for(int i; i<4; i++)
				{
					owner.A_SpawnItemEx("CCards_Actor_SentryProp", 16 + owner.radius / 2, angle: i * 90, flags:SXF_NOCHECKPOSITION);
				}
				
			}
		}
		else
		{
			owner.bSOLID = false;
			owner.speed = 0;
			owner.SetOrigin((anchorpos.x, anchorpos.y, owner.pos.z), true);
		}

		Super.Tick();
	}
}

class CCards_Actor_SentryProp : CCards_Actor_FX
{
	Default
	{
		+MISSILE;
		+FLOATBOB;
		FloatBobStrength 0.25;
		Scale 0.75;
	}

	states
	{
		Spawn:
		C_F3 AB 3
		{
			if(!target || target.CountInv("CCards_Actor_SentryActivator") == 0)
			{
				Destroy();
				return;
			}

			bBRIGHT = (target.health < 1);
			SetOrigin((pos.x, pos.y, target.pos.z + 8 + (target.height / 32)), true);
		}
		loop;
	}
}

Class CCard_MonsterLastWord : GenericCard
{
	override void Init()
	{
	Super.Init();
	effectMonsters = true;
	tier = 0;
	background = "CARB57";
	description = "";
	canStack = true;
	usespecies = true;
	AddTag("newprojectile");
	}

	uint calculateTier(int health)
	{
		if(health > 0)
		{
			return max(1, min(5, health / 120));
		}

		return 1;
	}

	Class<Actor> projectile;

	override void OnDeath(Actor a)
	{
		if(!projectile)
		{
			return;
		}

		let l = CCards_Actor_LastWordPortal(a.Spawn("CCards_Actor_LastWordPortal", a.pos + (0,0,a.default.height / 2)));

		if(!l)
		{
			return;
		}
		
		l.projectile = projectile;
		l.target = a;
		if(a.target)
		{
			l.tracer = a.target;
		}
		else
		{
			l.tracer = l;
		}
	}

	override bool Condition()
	{
		if(!projectile)
		{
			FindProjectile();
		}

		if(projectile)
		{
			if(monsterfigure.health > 500)
			{
				return false;
			}
			return true;
		}
		
		// Defaults for debug viewing
		projectile = "DoomImpBall";
		tier = 1;
		
		description = Stringtable.Localize("$" .. "CC_HINT_MONSTERLASTWORD1");
		description.Replace("#", GetMonsterName());
		description.Replace("@", "Imp");
		return false;
	}

	void FindProjectile()
	{
		if(projectile)
		{
			return;
		}
		Array<CCards_Monster> users;

		for(int i; i<game.global.foundMonsters.Size(); i++)
		{
			CCards_Monster pi = game.global.foundMonsters[i];
			if(pi.projectile)
			{
				users.Push(pi);
			}
		}

		while(users.Size()>0)
		{
			int r = game.global.RandomNumber(0, users.size()-1);

			if(GetDefaultByType(users[r].projectile).radius > 16 || GetDefaultByType(users[r].projectile).speed == 0)
			{
				users.Delete(r);
			}
			else
			{
				projectile = users[r].projectile;
				String monstername = GetDefaultByType(users[r].monsterClass).GetTag();
				tier = calculateTier(GetDefaultByType(users[r].monsterClass).health);
				SetDescription(monstername);
				return;
			}
		}		
	}

	virtual void SetDescription(String name)
	{
		description = Stringtable.Localize("$" .. "CC_HINT_MONSTERLASTWORD1");
		description.Replace("#", GetMonsterName());
		description.Replace("@", name);
		//description = GetMonsterName() .. " fire a " .. name .. " projectile on death.";
	}
}

Class CCards_Actor_LastWordPortal : Actor
{
	Default
	{
		+MISSILE;
		-SOLID;
		//+NOBLOCKMAP;
		+THRUACTORS;
		+NOGRAVITY;
		+ROLLSPRITE;
		+BRIGHT;
		renderstyle "STYLE_Translucent";
		alpha 0.0;
		scale 1.2;
		height 2;
		radius 2;
		speed 8;
		Translation "CCards_LastWord";
	}

	Class<Actor> projectile;

	states
	{
		Spawn:
		TNT1 A 8;
		TNT1 A 0
		{
			if(!projectile)
			{
				projectile = "DoomImpBall";
			}
			A_FaceTracer();
		}
		TNT1 A 0 A_PlaySound("corruptioncards/lastword", CHAN_VOICE);
		C_F2 ZZZZZZZZZZZZZZZZZZZZZZZ 1 
		{
			roll += 10.0;
			A_FadeIn(0.055);
			A_SetScale(scale.x - 0.05, scale.x - 0.05);
			if(projectile)
			{
				FLineTraceData d;
				if(LineTrace (0, GetDefaultByType(projectile).radius, 0, TRF_ALLACTORS, data:d))
				{
					if(d.HitActor)
					{
						if(!d.HitActor.player && (d.HitActor.bSOLID || d.HitActor.bSHOOTABLE))
						{
							SetOrigin(pos + (0,0,32), false);
						}
					}
				}
			}
		}
		TNT1 A 0
		{
			A_StopSound(CHAN_VOICE);
			A_PlaySound("corruptioncards/shockwave");
			roll = 0;
			A_SetScale(0.3, 0.3);

			A_FaceTracer();	

			if(tracer)
			{
				FireProjectile();
			}
			//SpawnProjectile(projectile);
		}
		C_F0 VWXYZ 2;
		stop;
	}

	virtual Actor FireProjectile()
	{
		Actor f = Actor(A_SpawnProjectile(projectile, 0, 0, 0, ptr:AAPTR_TRACER));

		if(f)
		{
			//f.GiveInventory("CCards_Token_NotProjectileThrower", 1); // This line crashes?
			CCards_Token_NotProjectileThrower t = CCards_Token_NotProjectileThrower(Spawn("CCards_Token_NotProjectileThrower"));
			t.AttachToOwner(f);
			f.target = target;
			if(Abs(f.Vel.x) == 0 && Abs(f.Vel.y) == 0)
			{
				let diff = Vec3To(tracer);
				float anglePitch = -atan2 (diff.Z, diff.XY.Length());
				f.Vel3DFromAngle(10, angle, anglePitch);
				f.A_Face(tracer);
			}

			return f;
		}

		return null;
	}
}


Class CCard_MonsterPromote : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = false;
	effectMonsters = true;
	background = "CARB66";
	canStack = false;
	AddTag("monsterstrength");
	}

	Array<GenericBuffCard> cards;
	
	override bool Condition()
	{
		tier = Min(tier + 3, 5);

		if(CCards_Functions.HasMapSpecial(monsterFigure.GetClass()))
		{
			return false;
		}

		for(int i; i<game.deck.Size(); i++)
		{
			GenericBuffCard gb = GenericBuffCard(game.deck[i]);
			if(gb)
			{
				if(gb.monsterType == monsterType)
				{
					continue;
				}

				if(cards.Find(gb) == cards.Size())
				{
					cards.push(gb);
				}
			}
		}

		if(cards.Size() < 3)
		{
			return false;
		}

		ThinkerIterator ti = ThinkerIterator.Create("CCards_MonsterThinker");
		CCards_MonsterThinker t;

		while (t = CCards_MonsterThinker(ti.Next()))
		{
			if(t.user)
			{
				if(t.user.health < monsterFigure.health)
				{
					return false;
				}
			}
		}

		return true;
	}

	override void OnSpawn(Actor a)
	{
		if(cards.Size() == 0)
		{
			return;
		}
		for(int i; i<cards.Size(); i++)
		{
			cards[i].Buff(a);
		}
	}
}

Class CCard_ExtraMonsters : GenericCard
{
	override void Init()
	{
	effectWorld = true;
	background = "CARB67";
	canStack = true;
	cardtag = TAG_WORLD;
	AddTag("startspawn");
	}

	int spawncount;

	override bool Condition()
	{
		if(CCards_Functions.HasMapSpecial(monsterType) || monsterfigure.bBOSS)
		{
			return false;
		}

		if(!HasGenericCard() && monsterfigure.health < 150)
		{
			return false;
		}

		return true;
	}

	bool HasGenericCard()
	{
		for(int i; i<game.global.collection.Size(); i++)
		{
			GenericCard g = GenericCard(game.global.collection[i]);

			if(g && g.monsterType == monsterType)
			{
				return true;
			}
		}

		return false;
	}

	override void OnGameStart(Actor a)
	{
		if(CCards_Functions.HasMapSpecial(monsterType))
		{
			spawncount = 0;
			return;
		}

		spawncount = Level.Total_Monsters * 0.25;
		spawncount = spawncount - (GetDefaultByType(monsterType).health / 30);
		spawncount = Clamp(spawncount, 5, 50);

		//Console.Printf(spawncount .. "");

		if(GetDefaultByType(monsterType).bBOSS)
		{
			spawncount = Min(3, spawncount);
		}
	}
	
	override void OnTick(Actor a)
	{
		if(spawncount > 0 && (Level.Time % 2) == 0)
		{
			CCards_Safespawner.New(monsterType.GetClassName(), spawncount, "CCards_Actor_SpawnFire", GetDefaultByType(monsterType).radius, GetDefaultByType(monsterType).height, 800, 4);
			
			spawncount = 0;
		}
	}
}


Class CCard_VesselMonster : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = true;
	effectMonsters = true;
	background = "CARB68";
	canStack = true;
	AddTag("monsterability");
	AddTag("newspawn");
	}
	
	Class<Actor> spawned;

	override bool Condition()
	{
		if(tier < 5)
		{
			tier = 5;
		}

		if(monsterFigure.health < 400 || 
		CCards_Functions.HasMapSpecial(monsterType) ||
		CCards_Functions.CountMonsterName(monsterType) > CCards_Functions.CountMonsters() * 0.05)
		{
			SetDebugDefaults();
			return false;
		}

		GetSpawnedMonster();

		if(!spawned)
		{
			SetDebugDefaults();
			return false;
		}

		String monstername = GetDefaultByType(spawned).GetTag();
		//tier = calculateTier(GetDefaultByType(users[r].monsterClass).health);
		//description = GetMonsterName() .. " spawns 3 " .. monstername .. " on death.";
		description = Stringtable.Localize("$" .. "CC_HINT_VESSELMONSTER1");
		description.Replace("#", GetMonsterName());
		description.Replace("@", monstername);

		return true;
	}

	void SetDebugDefaults()
	{
		spawned = "LostSoul";
		description = Stringtable.Localize("$" .. "CC_HINT_VESSELMONSTER1");
		description.Replace("#", GetMonsterName());
		description.Replace("@", "Lost Soul");
	}

	void GetSpawnedMonster()
	{
		ThinkerIterator ti = ThinkerIterator.Create("CCards_MonsterThinker");
		CCards_MonsterThinker t;

		Array<Class<Actor> > mons;

		while (t = CCards_MonsterThinker(ti.Next()))
		{
			if(t.user)
			{
				if(CCards_Functions.HasMapSpecial(t.user.GetClass()) || t.user.GetClass() == monsterType)
				{
					continue;
				}

				if(t.user.health < monsterFigure.health / 2 && t.user.health > monsterFigure.health / 8)
				{
					mons.Push(t.user.GetClass());
				}
			}
		}
		
		if(mons.Size() > 0)
		{
			spawned = mons[random(0, mons.Size() - 1)];
		}
	}

	override void OnDeath(Actor a)
	{
		if(a.CountInv("CCards_Token_VesselSpawn") > 0)
		{
			return;
		}
		CCards_VesselSpawn v = CCards_VesselSpawn(Actor.Spawn("CCards_VesselSpawn", a.pos));
		v.angle = a.angle;
		v.count = 3;
		v.spawnclass = spawned;
		v.target = a;
	}

}

class CCards_VesselSpawn : Actor
{
	Default
	{
		+NOINTERACTION;
		+NOGRAVITY;
		-SOLID;
	}

	Class<Actor> spawnclass;
	uint count;

	states
	{
		Spawn:
		TNT1 A 5;
		TNT1 A 13
		{
			A_StartSound("corruptioncards/vesselspawn");
			CCards_VesselFX v;
			v = CCards_VesselFX(Spawn("CCards_VesselFX", pos));
			v.target = self;
			if(target)
			{
				v.A_SetScale(v.scale.x + ((target.radius / 10) * 0.16), v.scale.y + ((target.radius / 10) * 0.16));
			}
			v = CCards_VesselFX(Spawn("CCards_VesselFX", pos));
			v.target = self;
			v.A_SetScale(0.9, 0.9);
			if(target)
			{
				v.A_SetScale(v.scale.x + ((target.radius / 10) * 0.16), v.scale.y + ((target.radius / 10) * 0.16));
			}
			v.dir = true;
		} //A_SpawnItemEx("CCards_VesselFX", 0, 0, 32);
		TNT1 A 10
		{
			for(int i; i<count; i++)
			{
				angle += 120;
				Actor a = Spawn(spawnclass, pos + (0,0,8));
				if(!a)
				{
					continue;
				}

				if(target && target.CountInv("CCards_Actor_MiniMonsterPowerup") > 0)
				{
					a.GiveInventory("CCards_Actor_MiniMonsterPowerup", 1);
				}

				a.GiveInventory("CCards_Token_VesselSpawn", 1);
				a.angle = angle;
				a.A_ChangeVelocity(10, 0, 0, CVF_RELATIVE);
				a.GiveInventory("CCards_Actor_SolidHelper", 1);
				if(target && target.target && target.target.player)
				{
					a.A_Face(target.target);
				}
				
				if(a.CheckBlock(CBF_NOACTORS))
				{
					a.A_DamageSelf(1);
				}

			}
		}
		stop;
	}

	override void Tick()
	{
		if(target)
		{
			Warp(target,0,0,0);
		}
		Super.Tick();
	}
}

class CCards_VesselFX : CCards_Actor_Smoke
{
	Default
	{
		scale 1.3;
		+BRIGHT;
		+MISSILE;
		alpha 1.0;
		Renderstyle "STYLE_Add";
		translation "CCards_Vessel";
	}

	states
	{
	Spawn:
	TNT1 A 0;
	TNT1 A 0
	{
		if(random(0,1) == 0)
		{
			//dir = true;
		}
		roll = random(0, 359.0);
	}
	Goto Fade;
	Fade:
    //C_F1 BBBCCDDDEEEFFFGGGHHH 1
	C_FX IIIJJJKKKLLLMMMMNNNNOOOO 1
	{
		if(target)
		{
			Warp(target,0,0,32);
		}
		A_FadeOut(0.005);
		A_SetScale(scale.x + 0.02, scale.y + 0.02);
		A_ScaleVelocity(0.9);
	}
    stop;
	}

	override void Tick()
	{
		A_SetRoll(dir ? roll + 4.7 : roll - 4.7);
		Super.Tick();
	}
}

class CCards_Token_VesselSpawn : CCards_Token
{}


Class CCard_MonsterFirstStrike : CCard_MonsterLastWord
{
	override void Init()
	{
	Super.Init();
	effectMonsters = true;
	tier = 0;
	background = "CARB69";
	description = "";
	canStack = true;
	usespecies = true;
	AddTag("newprojectile");
	}

	override void OnDeath(Actor a)
	{
	}

	override void OnSpawn(Actor a)
	{
		if(!projectile)
		{
			return;
		}

		let l = CCards_Actor_FirstStrikeInv(a.Spawn("CCards_Actor_FirstStrikeInv"));
		l.projectile = projectile;
		l.AttachToOwner(a);
	}

	override bool Condition()
	{	
		if(!projectile)
		{
			FindProjectile();
		}

		if(projectile)
		{
			if(CCards_Functions.CountMonsterName(monsterType) < 5)
			{
				return false;
			}
			return true;
		}
		
		// Defaults for debug viewing
		projectile = "DoomImpBall";
		tier = 1;
		description = Stringtable.Localize("$" .. "CC_HINT_MONSTERFIRSTSTRIKE1");
		description.Replace("#", GetMonsterName());
		description.Replace("@", "Imp Fireball");
		return false;
	}

	override void SetDescription(String name)
	{
		//description = GetMonsterName() .. " fire a " .. name .. " projectile upon seeing the player.";
		description = Stringtable.Localize("$" .. "CC_HINT_MONSTERFIRSTSTRIKE1");
		description.Replace("#", GetMonsterName());
		description.Replace("@", name);
	}
}

Class CCards_Actor_FirstStrikeInv : Inventory
{
	Class<Actor> projectile;
	uint seed;

	override void PostBeginPlay()
	{
		seed = random(0, 1000);
	}

	override void Tick()
	{
		if(owner && owner.target && owner.target.player)
		{
			if((Level.Time + seed) % 10 == 0 && owner.CheckSight(owner.target, SF_IGNOREVISIBILITY))
			{
				if(owner.health > 1)
				{
					let l = CCards_Actor_FirstStrikeShot(owner.Spawn("CCards_Actor_FirstStrikeShot", owner.pos + (0,0,owner.default.height / 2)));

					if(!l)
					{
						return;
					}
					
					l.projectile = projectile;
					l.target = owner;

					if(owner.target)
					{
						l.tracer = owner.target;
					}
					else
					{
						l.tracer = l;
					}
				}
				DetachFromOwner();
				Destroy();
				return;
			}
		}

		Super.Tick();
	}
}

Class CCards_Actor_FirstStrikeFX : CCards_Actor_FX	
{
	Default
	{
		+FLATSPRITE;
		+BRIGHT;
		scale 0.8;
	}
	states
	{
		Spawn:
		C_F3 R 1
		{
			pitch = -90;
			A_ScaleVelocity(0.7);
			if(GetAge() > 10)
			{
				A_FadeOut(0.1);
			}
		}
		loop;
	}
}

Class CCards_Actor_FirstStrikeShot : CCards_Actor_LastWordPortal
{
	Default
	{
		renderstyle "STYLE_Add";
		alpha 0.8;
		scale 1.0;
	}

	states
	{
		Spawn:
		TNT1 A 1;
		TNT1 A 0
		{
			if(!projectile)
			{
				projectile = "DoomImpBall";
			}
			A_FaceTracer();
		}
		TNT1 A 0 
		{
			A_PlaySound("corruptioncards/firststrike", CHAN_VOICE);
			if(target)
			{
				Warp(target, (target.radius / 2), 0, target.default.height /2);
				A_FaceTracer();
				A_SpawnItemEx("CCards_Actor_FirstStrikeFX", 12, 0, 0, 8);
			}
		}
		//TNT1 AAAAA 2;
		TNT1 A 15;
		TNT1 A 10
		{
			//A_StopSound(CHAN_VOICE);
			//A_PlaySound("corruptioncards/shockwave");

			A_FaceTracer();	
			WarpCenter();

			if(tracer && target && target.health > 0)
			{
				let f = FireProjectile();
				if(f)
				{
					f.A_ScaleVelocity(1.025);
				}
			}
			//SpawnProjectile(projectile);
		}
		stop;
	}

	void WarpCenter()
	{
		if(target)
		{
			Warp(target, (target.radius / 2) + (GetDefaultByType(projectile).radius / 2), 0, target.default.height /2);
		}
	}
}

Class CCard_MonsterBombardment : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = true;
	effectMonsters = true;
	background = "CARB79";
	canStack = false;
	cardtag = TAG_AUGMENT;
	AddTag("newprojectile");
	}
	
	Class<Actor> projectile;

	override void SetTier()
	{
		Super.SetTier();

		tier = min(tier + 2, 5);
	}

	override bool Condition()
	{
		let pmon = game.global.observer.FindProjectileThrower(monsterType.GetClassName());

		if(!pmon || pmon.unsafeProjectile)
		{
			projectile = "DoomImpBall";
			return false;
		}

		projectile = pmon.projectile;	
		
		return true;
	}

	override void OnSpawn(Actor a)
	{
		CCards_Actor_ProjectileBombardment ps = CCards_Actor_ProjectileBombardment(Actor.Spawn("CCards_Actor_ProjectileBombardment", a.pos));
		let pmon = game.global.observer.FindProjectileThrower(a.GetClassName());

		if(ps && projectile)
		{
			if(pmon && pmon.projectile)
			{
				ps.projectile = pmon.projectile;
			}
			else
			{
				ps.projectile = projectile;
			}
			ps.AttachToOwner(a);
		}
	}
}

class CCards_Actor_ProjectileBombardment : Inventory
{
	Class<Actor> projectile;
	uint counter;
	bool hasseen;

	override void Tick()
	{
		if(owner && owner.health > 0)
		{
			if(!hasseen)
			{
				if(level.Time % 35 == 0)
				{
					hasseen = owner.CheckSight(owner.target, SF_IGNOREVISIBILITY);
				}

				Super.Tick();
				return;
			}
			if(counter == 0)
			{
				counter = random(1, 42);
			}

			counter--;

			if(owner.target && owner.target.player && owner.target.health > 0)
			{
				if(counter == 6)
				{
					owner.A_SpawnItemEx("CCards_Actor_BombardmentFX");
				}

				if(counter <= 1)
				{
					counter = 64;
					FireProjectile();
				}
			}
		}
		Super.Tick();
	}

	void FireProjectile()
	{
		if(!owner || !owner.target)
		{
			return;
		}

		Actor f = Actor(owner.A_SpawnProjectile(projectile, owner.height/2, 0, 0, ptr:AAPTR_TARGET));

		if(f)
		{
			if(!f.default.seesound)
			{	
				f.A_PlaySound("corruptioncards/shockwave");
			}
			CCards_Token_NotProjectileThrower t = CCards_Token_NotProjectileThrower(Spawn("CCards_Token_NotProjectileThrower"));
			t.AttachToOwner(f);
			f.target = owner;
			if(Abs(f.Vel.x) == 0 && Abs(f.Vel.y) == 0)
			{
				let diff = Vec3To(owner.target);
				float anglePitch = -atan2 (diff.Z, diff.XY.Length());
				f.Vel3DFromAngle(10, angle, anglePitch);
				f.A_Face(owner.target);
			}
		}			
	}
}

class CCards_Actor_BombardmentFX : CCards_Actor_FX
{
	Default
	{
		+MISSILE;
		+BRIGHT;
		+FORCEXYBILLBOARD;
		translation "CCards_Bombardment";
		scale 0.25;
		alpha 0.5;
		//alpha 0.0;
	}

	override void Tick()
	{
		if(target)
		{
			Warp(target, target.radius / 2, 0, target.height / 2, flags:WARPF_NOCHECKPOSITION);
		}
		Super.Tick();
	}

	states
	{
		Spawn:
		C_F1 A 2;
		C_F0 ZYWXY 2;
		stop;
	}
}


Class CCard_MonsterInvertKnockback : GenericCard
{
	override void Init()
	{
	Super.Init();
	useSpecies = true;
	effectMonsters = true;
	background = "CARB84";
	canStack = false;
	AddTag("monsterstrength");
	}

	override void OnSpawn(Actor a)
	{
		a.GiveInventory("CCards_Actor_InvertKnockback", 1);
	}

	override bool Condition()
	{
		return game.global.observer.FindProjectileThrower(monsterType.GetClassName());
	}
}

Class CCards_Actor_InvertKnockback : Inventory
{
	override void ModifyDamage (int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if (!passive && damage > 0 && source && source.CountInv("CCards_Actor_InvertKnockbackCooldown") == 0)
		{
			int a = source.AngleTo(owner);
			int t = damage * 0.57;

			source.Thrust(t, a);
			source.GiveInventory("CCards_Actor_InvertKnockbackCooldown", 1);
		}
	}
}

Class CCards_Actor_InvertKnockbackCooldown : Powerup
{
	Default
	{
		Powerup.duration 2;
	}
}

Class CCard_GenericSpawnPods : GenericCard
{
	override void Init()
	{
	background = "CARB85";
	canStack = true;
	cardtag = TAG_TRAP;
	effectworld = true;
	usespecies = false;
	Addtag("spawnnew");
	}

	int spawncount;
	CCards_Actor_SafeSpawner sp;

	override bool Condition()
	{
		if(monsterFigure.bBOSS || CCards_Functions.HasMapSpecial(monsterType))
		{
			return false;
		}
		
		return true;
	}

	override void OnGameStart(Actor a)
	{
		if(isStackPrimary())
		{
			spawncount = Clamp(Level.Total_Monsters / 40, 1, 8);
			spawncount = spawncount * (stackIndex + 1);
			//Console.Printf("" .. spawncount);
		}
		else
		{
			spawncount = 0;
		}
	}
	
	CCards_SafespawnerCallback callback;

	override void OnTick(Actor a)
	{
		if(spawncount > 0 && (Level.Time % 2) == 0)
		{
			if(!sp)
			{
				sp = CCards_Safespawner.New("CCards_Actor_SpawnPod", 1, "", GetDefaultByType(monsterType).radius, GetDefaultByType(monsterType).height, 0, 1);
				spawncount--;
				callback = new ("CCards_SafespawnerCallback");
				sp.spawnercallback = callback;
			}
		}

		if(callback && callback.spawnedactor)
		{
			CCards_Actor_SpawnPod pod = CCards_Actor_SpawnPod(callback.spawnedactor);
			if(pod)
			{
				pod.tospawn = monsterType;
				callback = null;
			}
			sp = null;
		}
	}
}
